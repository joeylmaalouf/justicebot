#include <Wire.h>
#include <Adafruit_MotorShield.h>
#define FORWARD 1
#define BACKWARD 2
#define RELEASE 3

Adafruit_MotorShield MS;
Adafruit_DCMotor *leftMotor, *rightMotor;
char prev_data[2] = "h";
char serial_data[2] = "h";
int LOOP_DELAY = 100;
byte LEFT_MOTOR_PORT = 1, RIGHT_MOTOR_PORT = 2;
byte GREEN_LED = 9, RED_LED = 10;
int SENSOR_PIN = A0;
int LED_THRESHOLD = 300;
byte MOTOR_STEP = 10;
byte motor_full_power = 255, motor_no_power = 0;
int sensor_value;

void setup()
{
  MS = Adafruit_MotorShield();
  MS.begin();
  leftMotor = MS.getMotor(LEFT_MOTOR_PORT);
  rightMotor = MS.getMotor(RIGHT_MOTOR_PORT);
  leftMotor->setSpeed(motor_full_power);
  rightMotor->setSpeed(motor_full_power);
  leftMotor->run(BACKWARD);
  rightMotor->run(FORWARD);
  pinMode(GREEN_LED, OUTPUT);
  pinMode(RED_LED, OUTPUT);
  Serial.begin(9600);
}

void loop()
{
  sensor_value = analogRead(SENSOR_PIN);
  if (sensor_value > LED_THRESHOLD)
  {
    digitalWrite(GREEN_LED, LOW);
    digitalWrite(RED_LED, HIGH);
  }
  else
  {
    digitalWrite(GREEN_LED, HIGH);
    digitalWrite(RED_LED, LOW);
  }
  
  if (Serial.available())
  {
    memcpy(prev_data, serial_data, sizeof(serial_data));
    Serial.readBytes(serial_data, 1);
  }
  Serial.println(serial_data);

  // set motor direction if input is UP or DOWN
  if (serial_data[0] == 'u')
  {
    leftMotor->run(BACKWARD);
    rightMotor->run(FORWARD);
  }
  else if (serial_data[0] == 'd')
  {
    leftMotor->run(FORWARD);
    rightMotor->run(BACKWARD);
  }
  // adjust motor power if input is PGUP or PGDOWN
  else if (serial_data[0] == 'f')
  {
    motor_full_power = min(255, motor_full_power + MOTOR_STEP);
    set_power(prev_data[0]); // set powers based on previous input,
                             // since this one didn't change any direction
  }
  else if (serial_data[0] == 's')
  {
    motor_full_power = max(0, motor_full_power - MOTOR_STEP);
    set_power(prev_data[0]); // set powers based on previous input,
                             // since this one didn't change any direction
  }

  // set which motors have power based on the input
  set_power(serial_data[0]);

  delay(LOOP_DELAY);
}

void set_power(char data)
{
  // set motor powers to turn if input is LEFT or RIGHT,
  // stop if input is SPACE, or go straight if input is UP or DOWN
  if (data == 'l')
  {
    leftMotor->setSpeed(motor_no_power);
    rightMotor->setSpeed(motor_full_power);
  }
  else if (data == 'r')
  {
    leftMotor->setSpeed(motor_full_power);
    rightMotor->setSpeed(motor_no_power);
  }
  else if (data == 'h')
  {
    leftMotor->setSpeed(motor_no_power);
    rightMotor->setSpeed(motor_no_power);
  }
  else if (data == 'u' || data == 'd')
  {
    leftMotor->setSpeed(motor_full_power);
    rightMotor->setSpeed(motor_full_power);
  }
}

// https://www.arduino.cc/en/Reference/SerialEvent
// maybe use an event/callback system?
